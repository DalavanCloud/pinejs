Rule ::=
	Modifier
	RuleBody
	'.'?
RuleBody ::=
	(	Quantifier
		term
		(	'that'
			(	verb
				(	is-atomic-formulation
				|	RuleBody
				)
			|	RuleBody
			)
			','?
		)?
	|	'the'
		(	referenced-term
		|	name
		)
	)
	(	is-atomic-formulation
	|	verb
		(	is-atomic-formulation
		|	RuleBody
		)
	)
Modifier ::=
	(	'It is obligatory that'
	|	'It is necessary that'
	|	'It is prohibited that'
	|	'It is impossible that'
	|	'It is not possible that'
	|	'It is possible that'
	|	'It is permissible that'
	)
Quantifier ::=
		(	'each'
		|	'a'
		|	'an'
		|	'some'
		)
	|	(
		|	'at most'
		|	'at least'
			(Number 'and at most')?
		|	'more than'
		|	'exactly'
		)
		Number

Number ::=
		'one'
	|	[0-9]+

Attribute ::=
	Definition
Definition ::=
		'the'?
		term
		'that'
		RuleBody
		'.'?
	|	value
		(	'or'
			value
		)*
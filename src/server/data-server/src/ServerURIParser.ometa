define(['ometa/ometa-base'], function() {
	var ServerURIParser;
	ometa ServerURIParser{
		word =
			<	(	letter
				|	'-'
				)+
			>,

		nmbr =
			<digit+>:number
			-> parseInt(number, 10),

		part =
			<	(	letter
				|	digit
				|	'-'
				|	'_'
				)+
			>,

		mdtp =
				seq('filt')
			|	seq('sort'),

		parm =
			part:t
			(	'.'
				word
			|	{t}:f
				{[]}:t
				-> f
			):f
			(	'='
				-> 'eq'
			|	seq('!=')
				-> 'ne'
			|	'~'
				-> 'lk'
			):o
			part:v
			';'?
			-> [o,t,f,v],

		actn =
			(	'*'
				mdtp:a
				(	':'
					parm+
				|	-> []
				):p
				-> [a].concat(p)
			)+
			| -> [],
		comm =
				seq('cr')
			|	seq('execute'),
		Term =
			<	(	letter
				|	'_'
				)+
			>,
		Verb =
			<	(	letter
				|	'_'
				)+
			>,

		trmf =
			Term:t
			-> ['Term', t],

		FactType =
			{['terms']}:ts
			{''}:ft
			(	Term:t
				'-'
				Verb:v
				{ts.push(t)}
				{ft + t + "-" + v}:ft
			)+ 
			(	'-'
				Term:t
				{ts.push(t)}
				{ft + "-" + t}:ft
			)?
			-> ['FactType', ft, ts], 

		id =
			(	'.'
				nmbr:n
				-> [n]
			|	-> ['']
			),

		 uri =
			'/' word:w '/'? 
			(
				(	FactType
				|	trmf
				):f
				id:i
				actn:a
				{['mod'].concat(a)}:a
				'/'?
				(	comm:c
					{a.push([c])}
				)?
				{f.concat(i)}:f
				{f.push(a)}
				-> [w].concat([f])
			|	-> [w]
			)
	}

	return ServerURIParser;
});
define(['ometa/ometa-base'], function() {
	var ServerURIParser;
	ometa ServerURIParser{
		word =
			<	(	letter
				|	'_'
				)+
			>,

		nmbr =
			<digit+>:number
			-> parseInt(number, 10),

		part =
			<	(	letter
				|	digit
				|	'-'
				|	'_'
				)+
			>,

		mdtp =
				seq('filt')
			|	seq('sort'),

		parm =
			part:t
			(	'.'
				word
			|	{t}:f
				{[]}:t
				-> f
			):f
			(	'='
				-> 'eq'
			|	seq('!=')
				-> 'ne'
			|	'~'
				-> 'lk'
			):o
			part:v
			';'?
			-> [o,t,f,v],

		actn =
			(	'*'
				mdtp:a
				(	':'
					parm+
				|	-> []
				):p
				-> [a].concat(p)
			)+
			| -> [],
		Command =
				seq('cr')
			|	seq('execute'),

		Term =
			word:term
			-> ['Term', term],

		FactType =
			{['terms']}:terms
			{''}:factType
			(	word:term
				'-'
				word:verb
				{terms.push(term)}
				{factType + term + "-" + verb}:factType
			)+ 
			(	'-'
				word:term
				{terms.push(term)}
				{factType + "-" + term}:factType
			)?
			-> ['FactType', factType, terms], 

		id =
			(	'.'
				nmbr:n
				-> [n]
			|	-> ['']
			),

		 uri =
			'/' word:w '/'? 
			(
				(	FactType
				|	Term
				):f
				id:i
				actn:a
				{['mod'].concat(a)}:a
				'/'?
				(	Command:c
					{a.push([c])}
				)?
				{f.concat(i)}:f
				{f.push(a)}
				-> [w].concat([f])
			|	-> [w]
			)
	}

	return ServerURIParser;
});
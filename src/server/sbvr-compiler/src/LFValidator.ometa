define(['sbvr-parser/SBVRLibs', 'ometa/ometa-base'], function(SBVRLibs) {
	var LFValidator;
	
	// TODO: Make the LFValidator validate that it is actually correctly structured LF.
	ometa LFValidator <: SBVRLibs {
		$ :x =
			(	token(x):a
				-> [a] 
			|	-> []
			),

		trans
			[:t apply(t):a]
			-> a,
		token :x =
			[:t ?{t==x} apply(x):a]
			-> a,

		letters =
			letter+:l space*
			-> l.join(''),

		num
			number:n ?{!isNaN(n)}
			-> ['num', parseInt(n)],

		Model
			{[]}:xs
			(
				(	"Term"
				|	"FactType"
				|	"rule"
				):x
				(	?(x != null)
					{xs.push(x)}
				)?
			)*
			-> ['Model'].concat(xs),

		FactType
			{[]}:factType
			(	"Term":t
				"Verb":v
				{factType.concat([t,v])}:factType
			)*
			$('Term'):t
			{factType.concat(t)}:factType
			&(:attrs AddFactType(factType, factType))?
			addAttributes(['FactType'].concat(factType)),

		Term
			:t addAttributes(['Term', t]),
		Verb
			:v
			-> ['Verb', v],
		rule
			("obl"|"nec"|"pos"|"prm"):x "text":t
			-> ['rule', x, t],
		addAttributes :termOrVerb =
			(	end
			|	{{}}:attrsFound
				[	(
						[	:attrName {attrsFound[attrName] = true}
							(	?(this['attr'+attrName]) {this._applyWithArgs.call(this, 'attr'+attrName, termOrVerb)}
							|	anything
							):attrVal
						]
						-> [attrName, attrVal]
					)*:attrs
					end
				]
				defaultAttributes(termOrVerb, attrsFound, attrs)
			)
			-> termOrVerb,
		attrDefinition :termOrVerb =
			(	['Enum' :values]
			|	trans
			),
		attrSynonymousForm :factType =
			:synForm
			AddFactType(synForm.slice(0, -1), factType.slice(1))
			-> synForm,

		text
			:a
			-> ['text', a],
		
		obl
			trans*:xs
			-> ['obl'].concat(xs),
		nec
			trans*:xs
			-> ['nec'].concat(xs),
		pos
			trans*:xs
			-> ['pos'].concat(xs),
		prm
			trans*:xs
			-> ['prm'].concat(xs),

		neg
			trans:xs
			-> ['neg'].concat([xs]),

		quant =
			(	"univQ"
			|	"existQ"
			|	"exactQ"
			|	"atMostQ"
			|	"atLeastQ"
			|	"numRngQ"
			),
		univQ
			"var":v trans*:xs
			-> ['univQ', v].concat(xs),
		existQ
			"var":v trans*:xs
			-> ['existQ', v].concat(xs),
		exactQ
			"card":i
			"var":v trans*:xs
			-> ['exactQ', i, v].concat(xs),
		atMostQ
			"maxCard":a
			"var":v trans*:xs
			-> ['atMostQ', a, v].concat(xs),
		atLeastQ
			"minCard":i
			"var":v trans*:xs
			-> ['atLeastQ', i, v].concat(xs),
		numRngQ
			"minCard":i "maxCard":a
			"var":v trans*:xs
			-> ['numRngQ', i, a, v].concat(xs),

		card
			"num":n
			-> ['card', n],
		minCard
			"num":n
			-> ['minCard', n],
		maxCard
			"num":n
			-> ['maxCard', n],

		var
			"num":n "Term":t ("AtomicFormulation"|quant):w
			-> ['var', n, t, w],
		var
			"num":n "Term":t
			-> ['var', n, t],

		bind
			"Term":t number:n
			-> ['bind', t, n], 
		AtomicFormulation
			"FactType":f "bind"*:b
			-> ['AtomicFormulation', f].concat(b)
	}
	
	LFValidator.initialize = function() {
		SBVRLibs.initialize.call(this);
	}

	LFValidator.defaultAttributes = function(termOrVerb, attrsFound, attrs) {
		termOrVerb.push(attrs);
	}
	
	return LFValidator;
});